
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОпределитьСлой();
	ЗаполнитьВспомогательныеРеквизиты();
КонецПроцедуры

&НаСервере
Процедура ОпределитьСлой()
	Слои = Новый Массив;
	
	Слои.Добавить("Слой_Комментарий");
	Слои.Добавить("Слой_ВложенныйПроцесс");
	Слои.Добавить("Слой_ВариантУсловия");
	Слои.Добавить("Слой_Условие");
	Слои.Добавить("Слой_ПользовательскаяИстория");
	Слои.Добавить("Слой_Завершение");
	Слои.Добавить("Слой_Старт");
	
	Для Каждого Слой Из Слои Цикл
		ИмяЭлементаПроцесса = СтрЗаменить(Слой, "Слой_", "");
		
		Элементы[Слой].Видимость = Перечисления.ЭлементыПроцесса[ИмяЭлементаПроцесса] = Объект.ЭлементПроцесса;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВспомогательныеРеквизиты()
	Персона = Объект.Персона;
	КлючевоеДействие = Объект.ПользовательскаяИстория.КлючевоеДействие;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ТекущийОбъект.ЭлементПроцесса = Перечисления.ЭлементыПроцесса.ПользовательскаяИстория Тогда
		ДанныеПользовательскойИстории = Новый Структура;
		
		ДанныеПользовательскойИстории.Вставить("Персона", Объект.Персона);
		ДанныеПользовательскойИстории.Вставить("КлючевоеДействие", КлючевоеДействие);
		ДанныеПользовательскойИстории.Вставить("Владелец", Объект.Владелец);
		
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ДанныеПользовательскойИстории", ДанныеПользовательскойИстории);
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("УстановитьКурсорНаНовыйЭлементПроцесса", Объект.Ссылка);
КонецПроцедуры
