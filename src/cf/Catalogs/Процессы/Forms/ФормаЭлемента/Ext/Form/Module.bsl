
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОпределитьСлой();
КонецПроцедуры

&НаСервере
Процедура ОпределитьСлой()
	Слои = Новый Массив;
	
	Слои.Добавить("Слой_Комментарий");
	Слои.Добавить("Слой_ВложенныйПроцесс");
	Слои.Добавить("Слой_ВариантУсловия");
	Слои.Добавить("Слой_Условие");
	Слои.Добавить("Слой_ПользовательскаяИстория");
	Слои.Добавить("Слой_Завершение");
	Слои.Добавить("Слой_Старт");
	
	Для Каждого Слой Из Слои Цикл
		ИмяЭлементаПроцесса = СтрЗаменить(Слой, "Слой_", "");
		
		Элементы[Слой].Видимость = Перечисления.ЭлементыПроцесса[ИмяЭлементаПроцесса] = Объект.ЭлементПроцесса;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("УстановитьКурсорНаНовыйЭлементПроцесса", Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПерсоной(Команда)
	Объект.Представление = Строка(Объект.Персона);
КонецПроцедуры

&НаКлиенте
Процедура ПользовательскаяИсторияПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.ПользовательскаяИстория) Тогда
		ЗаполнитьПерсонуИКлючевоеДействиеНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПерсонуИКлючевоеДействиеНаСервере()
	ОбъектНаСервере = РеквизитФормыВЗначение("Объект");

	ЗаполнитьЗначенияСвойств(ОбъектНаСервере, ОбъектНаСервере.ПользовательскаяИстория, "Персона,КлючевоеДействие");
	
	ЗначениеВРеквизитФормы(ОбъектНаСервере, "Объект");
КонецПроцедуры
