
Процедура ПриКопировании(ОбъектКопирования)
	ОчиститьРеквизитыПриКопировании();
КонецПроцедуры

Процедура ОчиститьРеквизитыПриКопировании() 
	ПутьКФиче = "";
	Ценность = Неопределено;
	ОбъемРабот = Неопределено;
	БизнесВес = 0;
	НомерФичи = "";
КонецПроцедуры

Процедура ОпределитьНомерФичи()
	ЭтотОбъект.УстановитьНовыйКод();
	
	НомерФичи = XMLСтрока(Код);
КонецПроцедуры

Процедура ОпределитьПутьКФиче()
	Если ЗначениеЗаполнено(ПутьКФиче) Тогда
		Возврат;
	КонецЕсли;
	
	КаталогПроекта = ОбщегоНазначенияСервер.ПолучитьКаталогПроекта(Владелец);
	ИмяФичи = ОбщегоНазначенияКлиентСервер.ПолучитьИмяПеременнойИзСтроки(Заголовок);
	Расширение = ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеФичи();
	
	ПутьКФиче = КаталогПроекта + "\" + ИмяФичи + Расширение;
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Если ОшибкиВЗаполнение() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	ОпределитьНомерФичи();
	ОпределитьПутьКФиче();
КонецПроцедуры

Функция ОшибкиВЗаполнение()
	Результат = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПользовательскиеИстории.Представление КАК Представление
	|ИЗ
	|	Справочник.ПользовательскиеИстории КАК ПользовательскиеИстории
	|ГДЕ
	|	ПользовательскиеИстории.Заголовок ПОДОБНО &Заголовок
	|	И ПользовательскиеИстории.Ссылка <> &Ссылка";
	
	Запрос.УстановитьПараметр("Заголовок", Заголовок);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ТекстСообщения =
	"Найдены пользовательские истории с идентичным заголовком:
	|";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Результат = Истина;
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			ТекстСообщения = ТекстСообщения +
			"" + ВыборкаДетальныеЗаписи.Представление + "
			|";
		КонецЦикла;
		
	КонецЕсли;
	
	Если Результат Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстСообщения;
		Сообщение.Сообщить();	
	КонецЕсли;

	Возврат Результат;
КонецФункции
