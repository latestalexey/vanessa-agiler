
Процедура ПриКопировании(ОбъектКопирования)
	ОчиститьРеквизитыПриКопировании();
КонецПроцедуры

Процедура ОчиститьРеквизитыПриКопировании() 
	ПутьКФиче = "";
	Ценность = Неопределено;
	ОбъемРабот = Неопределено;
	БизнесВес = 0;
	НомерФичи = "";
КонецПроцедуры

Процедура ОпределитьНомерФичи()
	ЭтотОбъект.УстановитьНовыйКод();
	
	НомерФичи = XMLСтрока(Код);
КонецПроцедуры

Процедура ОпределитьПутьКФиче()
	Если ЗначениеЗаполнено(ПутьКФиче) Тогда
		Возврат;
	КонецЕсли;
	
	КаталогПроекта = ОбщегоНазначенияСервер.ПолучитьКаталогПроекта(Владелец);
	ИмяФичи = ОбщегоНазначенияКлиентСервер.ПолучитьИмяПеременнойИзСтроки(Заголовок);
	Расширение = ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеФичи();
	
	ПутьКФиче = КаталогПроекта + "\" + ИмяФичи + Расширение;
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Если ЭтоНовый() Тогда
		ОпределитьНомерФичи();
	КонецЕсли;

	Если ПустаяСтрока(Заголовок) Тогда
		Заголовок = "Как " + ?(ЗначениеЗаполнено(Персона), СокрЛП(Персона), "X") + " Хочу " + ?(ЗначениеЗаполнено(НеобходимыйФункционал), СокрЛП(НеобходимыйФункционал), "Y") + " Чтобы " + ?(ЗначениеЗаполнено(КлючевоеДействие), СокрЛП(КлючевоеДействие), "Z");
	КонецЕсли;
	
	Если ПустаяСтрока(ПутьКФиче) Тогда
		ОпределитьПутьКФиче();
	КонецЕсли;
КонецПроцедуры
